buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
    }
}

plugins {
    id 'java'
    id 'application'
    id "de.undercouch.download" version "3.3.0"
    id 'io.franzbecker.gradle-lombok' version '1.11'
}

apply plugin: 'org.junit.platform.gradle.plugin'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

applicationDefaultJvmArgs = ["-Djava.library.path=audio"]

mainClassName = "io.thoughtworksarts.riot.Main"

repositories {
    jcenter()
}

import de.undercouch.gradle.tasks.download.Download

def downloads = [
        [name: 'AsioHost', dest: 'asio/JAsioHost.jar',
            url: 'https://github.com/mhroth/jasiohost/raw/master/JAsioHost.jar' ],
        [name: 'AsioHostDll', dest: 'asio/jasiohost64.dll',
            url: 'https://github.com/mhroth/jasiohost/raw/master/jasiohost64.dll' ],
        [name: 'AsioForAllInstaller', dest: 'asio/ASIO4ALL_2_13_English.exe',
            url: 'http://tippach.business.t-online.de/asio4all/downloads_11/ASIO4ALL_2_13_English.exe' ],
        [name: 'Film', dest: 'src/main/resources/video/film.m4v',
            url: 'https://cloud.eldermael.io/s/Z32zvOg0TWI3poQ/download'],
        [name: 'Audio', dest: 'src/main/resources/audio/audio.wav',
            url: 'https://cloud.eldermael.io/s/atxubv2nFAAhnIb/download']
]

downloads.each {
    def file = it.get('name')
    def url = it.get('url')
    def destination = it.get('dest')
    
    task "download$file"(type: Download) {
        src url
        dest destination
        overwrite false
    }
}

compileJava.dependsOn tasks.withType(Download)

junitPlatformTest {
    def canonicalPathToLib = new File('asio/').getCanonicalPath();
    systemProperty 'java.library.path', "$canonicalPathToLib"
}

dependencies {

    compile files('asio/JAsioHost.jar')

    compile 'com.github.sarxos:webcam-capture:0.3.11'

    compile 'org.slf4j:slf4j-api:1.7.+'
    compile 'ch.qos.logback:logback-classic:1.0.13'

    compile 'com.github.sarxos:webcam-capture:0.3.11'
    compile 'org.deeplearning4j:deeplearning4j-core:0.9.1'
    compile 'org.deeplearning4j:deeplearning4j-modelimport:0.9.1'

    compileOnly 'org.projectlombok:lombok:1.16.18'

    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.2'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.0.2'

}