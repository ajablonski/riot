buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.2'
        classpath 'org.apache.ivy:ivy:2.3.0'
    }
}
plugins {
    id 'java'
    id 'application'
    id "de.undercouch.download" version "3.3.0"
    //id 'io.franzbecker.gradle-lombok' version '1.11'
    id 'org.openjfx.javafxplugin' version '0.0.7'
}

apply plugin: 'org.junit.platform.gradle.plugin'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

applicationDefaultJvmArgs = ["-Djava.library.path=asio"]

mainClassName = "io.thoughtworksarts.riot.Main"

repositories {
    jcenter()
}

javafx {
    version = "11.0.2"
    modules = [ 'javafx.controls', 'javafx.media' ]
}

import de.undercouch.gradle.tasks.download.Download

def downloads = [
        [name: 'AsioHost', dest: 'asio/JAsioHost.jar',
            url: 'https://github.com/mhroth/jasiohost/raw/master/JAsioHost.jar' ],
        [name: 'AsioHostDll', dest: 'asio/jasiohost64.dll',
            url: 'https://github.com/mhroth/jasiohost/raw/master/jasiohost64.dll' ],
        [name: 'AsioForAllInstaller', dest: 'asio/ASIO4ALL_2_13_English.exe',
            url: 'http://tippach.business.t-online.de/asio4all/downloads_11/ASIO4ALL_2_13_English.exe' ]
]

downloads.each {
    def file = it.get('name')
    def url = it.get('url')
    def destination = it.get('dest')
    
    task "download$file"(type: Download) {
        src url
        dest destination
        overwrite false
    }
}

compileJava.dependsOn tasks.withType(Download)

junitPlatformTest {
    def canonicalPathToLib = new File('asio/').getCanonicalPath();
    systemProperty 'java.library.path', "$canonicalPathToLib"
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

dependencies {

    compile files('asio/JAsioHost.jar')

    compile 'com.github.sarxos:webcam-capture:0.3.11'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.2.3'

    compile 'org.slf4j:slf4j-api:1.7.+'
    compile 'ch.qos.logback:logback-classic:1.0.13'

    compile 'com.github.sarxos:webcam-capture:0.3.11'
    compile 'org.apache.httpcomponents:httpclient:4.5.9'
    compile 'org.deeplearning4j:deeplearning4j-core:0.9.1'
    compile 'org.deeplearning4j:deeplearning4j-modelimport:0.9.1'
    compile 'org.nd4j:nd4j-native-platform:0.9.1'
    compile 'org.nd4j:nd4j-api:0.9.1'

    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'

    testCompile 'org.mockito:mockito-core:2.+'
    testCompile 'org.assertj:assertj-core:3.8.0'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.2'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.0.2'

}

